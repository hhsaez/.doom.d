#+title: Config
#+author: H. Hernan Saez

* Table of Contents :toc:
- [[#introduction][Introduction]]
- [[#user-setup][User Setup]]
- [[#editor][Editor]]
- [[#os-specific][OS Specific]]
  - [[#mac-os-x][Mac OS X]]
  - [[#window-wsl][Window WSL]]
- [[#frame-setup][Frame Setup]]
- [[#fonts][Fonts]]
- [[#theme][Theme]]
- [[#dashboard][Dashboard]]
- [[#misc][Misc]]
- [[#development][Development]]
  - [[#compilation][Compilation]]
  - [[#projectile][Projectile]]
  - [[#treemacs][Treemacs]]
- [[#gpt-el][GPT-EL]]

* Introduction
Place your private configuration here! Remember, you do not need to run 'doom sync' after modifying this file!


* User Setup
Some functionality uses this to identify you, e.g. GPG configuration, email clients, file templates and snippets. It is optional.

#+BEGIN_SRC emacs-lisp
;; (setq user-full-name "John Doe"
;;       user-mail-address "john@doe.com")
#+END_SRC

* Editor

Disable empty line markers

#+BEGIN_SRC emacs-lisp
(setq-default indicate-empty-lines nil)

(after! vi-tilde-fringe
  (global-vi-tilde-fringe-mode -1))
(remove-hook 'prog-mode-hook #'vi-tilde-fringe-mode)
#+END_SRC

Use the Command key as Meta on macOS while leaving Option for accented characters.

#+BEGIN_SRC emacs-lisp
(when (eq system-type 'darwin)
  (setq mac-command-modifier 'meta
        mac-option-modifier 'none
        ns-command-modifier 'meta
        ns-option-modifier 'none))
#+END_SRC

Revert buffers when the underlying file has changed.

#+BEGIN_SRC emacs-lisp
(global-auto-revert-mode 1)
#+END_SRC

By default Emacs will close all windows when pressing the ESC key. I hate that.

Instead, map the ESC key to keyboard-escape-quit (C-g) instead

#+BEGIN_SRC emacs-lisp
(define-key key-translation-map (kbd "ESC") (kbd "C-g"))
#+END_SRC

In addition, this seems to make the minibuffer to exit with just one ESC key press (instead of ESC ESC ESC by default)

* OS Specific

** Mac OS X

Restore the default macOS font zoom shortcuts now that Command behaves as Meta.

#+BEGIN_SRC emacs-lisp
(when (eq system-type 'darwin)
  (map! "M-=" #'doom/increase-font-size
        "M-+" #'doom/increase-font-size
        "M--" #'doom/decrease-font-size
        "M-_" #'doom/decrease-font-size
        "M-0" #'doom/reset-font-size))
#+END_SRC

** Window WSL

#+BEGIN_SRC emacs-lisp
(defun hhsaez/wsl-p ()
  (and (eq system-type 'gnu/linux)
       (or (getenv "WSL_INTEROP")
           (file-exists-p "/proc/sys/fs/binfmt_misc/WSLInterop"))))

(defun hhsaez/apply-borderless-frame ()
  (when (hhsaez/wsl-p)
    (dolist (param '((undecorated . t)
                     (drag-internal-border . 10)
                     (internal-border-width . 10)
                     (alpha-background . 100)))
      (setf (alist-get (car param) default-frame-alist) (cdr param))
      (add-to-list 'initial-frame-alist param))))

(add-hook 'after-init-hook #'hhsaez/apply-borderless-frame)

 (defun hhsaez/toggle-undecorated-frame ()
    "Toggle window-manager decorations on the current frame."
    (interactive)
    (let* ((frame (selected-frame))
           (current (frame-parameter frame 'undecorated)))
      (set-frame-parameter frame 'undecorated (not current))))
#+END_SRC

* Frame Setup
#+BEGIN_SRC emacs-lisp
(setq frame-resize-pixelwise t
      window-resize-pixelwise t)

(defun hhsaez/scale-and-center-frame (&optional frame)
  "Resize FRAME to 80%x90% of its monitor and center it. Works for new frames too."
  (let* ((frame (or frame (selected-frame))))
    (when (and (display-graphic-p frame)
               (fboundp 'frame-monitor-attributes))
      (let* ((attrs (frame-monitor-attributes frame))
             (workarea (alist-get 'workarea attrs)))
        (when (and workarea (>= (length workarea) 4))
          (pcase-let* ((`(,wx ,wy ,ww ,wh) workarea)
                       (frame-width (floor (* ww 0.8)))
                       (frame-height (floor (* wh 0.9)))
                       (left (+ wx (/ (- ww frame-width) 2)))
                       (top (+ wy (/ (- wh frame-height) 2))))
            (set-frame-size frame frame-width frame-height t)
            (set-frame-position frame (round left) (round top))))))))

(add-hook 'after-init-hook #'hhsaez/scale-and-center-frame t)
(add-hook 'after-make-frame-functions #'hhsaez/scale-and-center-frame)
#+END_SRC

* Fonts
Doom exposes five (optional) variables for controlling fonts in Doom:

- `doom-font' -- the primary font to use
- `doom-variable-pitch-font' -- a non-monospace font (where applicable)
- `doom-big-font' -- used for `doom-big-font-mode'; use this for
   presentations or streaming.
- `doom-symbol-font' -- for symbols
- `doom-serif-font' -- for the `fixed-pitch-serif' face

See 'C-h v doom-font' for documentation and more examples of what they accept. For example:

#+BEGIN_SRC emacs-lisp
;;n(setq doom-font (font-spec :family "Fira Code" :size 12 :weight 'semi-light)
;;      doom-variable-pitch-font (font-spec :family "Fira Sans" :size 13))
(setq doom-font (font-spec :family "JetBrains Mono" :size 16)
      doom-symbol-font (font-spec :family "Noto Sans Symbols" :size 16))
;;
;; If you or Emacs can't find your font, use 'M-x describe-font' to look them
;; up, `M-x eval-region' to execute elisp code, and 'M-x doom/reload-font' to
;; refresh your font settings. If Emacs still can't find your font, it likely
;; wasn't installed correctly. Font issues are rarely Doom issues!
#+END_SRC

* Theme
There are two ways to load a theme. Both assume the theme is installed and
available. You can either set `doom-theme' or manually load a theme with the
`load-theme' function. This is the default:

#+BEGIN_SRC emacs-lisp
(setq doom-theme 'doom-one)
#+END_SRC

* Dashboard

#+BEGIN_SRC emacs-lisp
(setq +doom-dashboard-banner-file (concat doom-user-dir "wallpaper.png"))
#+END_SRC

* Misc
#+BEGIN_SRC emacs-lisp
;; This determines the style of line numbers in effect. If set to `nil', line
;; numbers are disabled. For relative line numbers, set this to `relative'.
(setq display-line-numbers-type t)

;; If you use `org' and don't want your org files in the default location below,
;; change `org-directory'. It must be set before org loads!
(setq org-directory "~/org/")


;; Whenever you reconfigure a package, make sure to wrap your config in an
;; `after!' block, otherwise Doom's defaults may override your settings. E.g.
;;
;;   (after! PACKAGE
;;     (setq x y))
;;
;; The exceptions to this rule:
;;
;;   - Setting file/directory variables (like `org-directory')
;;   - Setting variables which explicitly tell you to set them before their
;;     package is loaded (see 'C-h v VARIABLE' to look up their documentation).
;;   - Setting doom variables (which start with 'doom-' or '+').
;;
;; Here are some additional functions/macros that will help you configure Doom.
;;
;; - `load!' for loading external *.el files relative to this one
;; - `use-package!' for configuring packages
;; - `after!' for running code after a package has loaded
;; - `add-load-path!' for adding directories to the `load-path', relative to
;;   this file. Emacs searches the `load-path' when you load packages with
;;   `require' or `use-package'.
;; - `map!' for binding new keys
;;
;; To get information about any of these functions/macros, move the cursor over
;; the highlighted symbol at press 'K' (non-evil users must press 'C-c c k').
;; This will open documentation for it, including demos of how they are used.
;; Alternatively, use `C-h o' to look up a symbol (functions, variables, faces,
;; etc).
;;
;; You can also try 'gd' (or 'C-c c d') to jump to their definition and see how
;; they are implemented.
#+END_SRC

* Development

The following allos code blocks within .org files to be formatted when using /C-c '/

#+BEGIN_SRC emacs-lisp

;; Good Org src defaults (indent inside blocks with c++-mode rules)
(after! org
  (setq org-src-tab-acts-natively t
        org-src-preserve-indentation nil
        org-edit-src-content-indentation 0))

;; Make org-src C++ edit buffers format like .cpp, without clang-format
(after! org-src
  (defun my/org-src-cpp-lsp-format-setup ()
    (when (and (bound-and-true-p org-src-mode)
               (derived-mode-p 'c++-mode))
      ;; give buffer a plausible filename/dir so clangd attaches (you already did this)
      (unless buffer-file-name
        (setq-local buffer-file-name
                    (expand-file-name "org-src-edit.cpp" default-directory)))

      ;; ensure LSP is running
      (when (fboundp 'lsp-deferred) (lsp-deferred))

      ;; 1) Format on save (optional)
      (add-hook 'before-save-hook #'lsp-format-buffer nil t)

      ;; 2) Use TAB to format (instead of cc-mode indent) in the edit buffer
      (local-set-key (kbd "TAB") #'lsp-format-buffer)

      ;; If you still want cc-mode indent on Shift+TAB:
      (local-set-key (kbd "<backtab>") #'indent-for-tab-command)

      ;; If you want to try on-type formatting anyway:
      ;; (setq-local lsp-enable-on-type-formatting t)
      ;; Note: may be limited or interfered by electric-indent-mode.
      ))
  (add-hook 'org-src-mode-hook #'my/org-src-cpp-lsp-format-setup))
#+END_SRC

** Compilation
Keep compilation buffers (including projectile builds) scrolled to the newest output.

#+BEGIN_SRC emacs-lisp
(after! compile
  (setq compilation-scroll-output t)

  (defun my/next-error-prioritize-errors (orig-fn &rest args)
    "Prefer diagnostics with severity â‰¥ error when cycling with `next-error'."
    (let ((orig-threshold compilation-skip-threshold))
      (condition-case _err
          (let ((compilation-skip-threshold (max 2 (or compilation-skip-threshold 0)))
                (inhibit-message t)
                (ring-bell-function #'ignore))
            (apply orig-fn args))
        (user-error
         (let ((compilation-skip-threshold orig-threshold))
           (apply orig-fn args))))))

  (unless (advice-member-p #'my/next-error-prioritize-errors 'next-error)
    (advice-add 'next-error :around #'my/next-error-prioritize-errors)))
#+END_SRC

** Projectile
Treat git submodules as part of the parent project instead of switching roots. The helper below asks git for the superproject directory; if we are inside a submodule it returns the top-level checkout so projectile keeps that as the active project. When no superproject exists the fallback still lets projectile use its usual detection.

#+BEGIN_SRC emacs-lisp
(after! projectile
  (require 'subr-x)
  (defun my/projectile-root-superproject (dir)
    "Return the superproject root for DIR when inside a git submodule."
    (when dir
      (let ((default-directory dir))
        (let* ((raw (when (executable-find "git")
                      (ignore-errors
                        (car (process-lines "git" "rev-parse" "--show-superproject-working-tree")))))
               (raw (and raw (string-trim raw)))
               (candidate (cond
                           ((and raw (not (string-empty-p raw)))
                            (if (file-name-absolute-p raw)
                                raw
                              (expand-file-name raw)))
                           (t (locate-dominating-file dir ".gitmodules"))))
               (resolved (and candidate
                              (file-name-as-directory (expand-file-name candidate)))))
          (when (and resolved (not (string-suffix-p "/" resolved)))
            (setq resolved (concat resolved "/")))
          (when (and resolved
                     (not (string-equal (directory-file-name resolved)
                                        (directory-file-name (expand-file-name dir)))))
            resolved)))))
  (add-to-list 'projectile-project-root-functions #'my/projectile-root-superproject))
#+END_SRC

** Treemacs

#+BEGIN_SRC emacs-lisp
(after! treemacs
  (setq treemacs-is-never-other-window nil))
#+END_SRC

** Tree-sitter grammars
Install missing C/C++ grammars automatically so tree-sitter works on fresh setups.

#+BEGIN_SRC emacs-lisp
(after! treesit
  (require 'cl-lib)
  (defvar hhsaez/treesit-auto-grammars '(c cpp)
    "Tree-sitter grammars that must be present locally.")

  (defvar hhsaez/treesit-language-sources
    '((c . ("https://github.com/tree-sitter/tree-sitter-c"))
      (cpp . ("https://github.com/tree-sitter/tree-sitter-cpp")))
    "Alist of LANG . SOURCE tuples to ensure before installing grammars.")

  ;; Guarantee treesit knows how to fetch our required grammars.
  (dolist (entry hhsaez/treesit-language-sources)
    (cl-pushnew entry treesit-language-source-alist
                :test (lambda (a b) (eq (car a) (car b)))))

  (defun hhsaez/ensure-treesit-grammars (&optional languages)
    "Install any missing tree-sitter LANGUAGES without prompting."
    (when (and (fboundp 'treesit-install-language-grammar)
               (fboundp 'treesit-language-available-p))
      (dolist (lang (or languages hhsaez/treesit-auto-grammars))
        (unless (treesit-language-available-p lang)
          (condition-case err
              (progn
                (message "Installing tree-sitter grammar for %s..." lang)
                (treesit-install-language-grammar lang))
            (error
             (message "tree-sitter grammar install failed for %s: %s" lang err)))))))

  (hhsaez/ensure-treesit-grammars))
#+END_SRC

* GPT-EL

#+BEGIN_SRC emacs-lisp
(after! gptel
  (require 'gptel-gh)
  (setq gptel-backend
        (gptel-make-gh-copilot "copilot-chat")
        gptel-model 'gpt-4o))
#+END_SRC
